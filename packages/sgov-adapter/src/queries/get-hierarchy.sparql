# see https://github.com/sstenchlak/schema-generator/issues/10
DEFINE sql:signal-void-variables 0

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX z: <https://slovník.gov.cz/základní/pojem/>

CONSTRUCT {
	?element a z:typ-objektu ;
		rdfs:subClassOf ?elementParent ;
		skos:prefLabel ?label ;
		skos:definition ?definition ;
		skos:inScheme ?glossary ;
		<__is_ciselnik> ?is_ciselnik .
} WHERE {
	{
		{%NODE% rdfs:subClassOf* ?element .}
		UNION
        {?element rdfs:subClassOf* %NODE% .}

		BIND(EXISTS {?element rdfs:subClassOf+ <https://slovník.gov.cz/datový/číselníky/pojem/položka-číselníku>} as ?is_ciselnik)

		FILTER NOT EXISTS {
			?element skos:inScheme <https://slovník.gov.cz/základní/glosář> .
		}

		OPTIONAL {
			?element rdfs:subClassOf ?elementParent .
			?elementParent skos:inScheme [] .
			FILTER NOT EXISTS {
				?elementParent skos:inScheme <https://slovník.gov.cz/základní/glosář> .
			}
		}
	}

    ?element a z:typ-objektu .
	OPTIONAL { ?element skos:prefLabel ?label . }
	OPTIONAL { ?element skos:definition ?definition }
	OPTIONAL { ?element skos:scopeNote ?definition }
	OPTIONAL { ?element skos:inScheme ?glossary }
}
