PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  <__search_results> <__has_search_results> ?property .
  ?property a wd:Q21503250 ;
            rdfs:label ?propertyLabel ;
            schema:description ?propertyDescription ;
            rdfs:domain ?subjectConstraint ;
            wikibase:propertyType ?type .
} WHERE {
  ?property p:P2302 ?statement ;
            wikibase:propertyType ?type .
  ?statement ps:P2302 wd:Q21510865 ;
             pq:P2308 %CLASS% .
  OPTIONAL {
    ?property p:P2302 ?statementSecond .
    ?statementSecond ps:P2302 wd:Q21503250 ;
                     pq:P2308 ?subjectConstraint .
  }
  OPTIONAL { 
    ?property schema:description ?propertyDescription . 
    FILTER(LANG(?propertyDescription) = "en")
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}


